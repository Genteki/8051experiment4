
LED1 EQU 40H
LED2 EQU 41H
LED3 EQU 42H
LED4 EQU 43H
NUM EQU 50H
TEMP1 EQU 51H
TEMP2 EQU 52H

ORG 0000H
LJMP START
ORG 0200H

START:
MOV R7, #4
MOV R0, #40H
INIT_LOOP:
    MOV @R0, #0
    INC R0
    DJNZ R7, INIT_LOOP
MOV NUM, #0
JMP MAIN
;******* 主程序 *******
MAIN:
LCALL TRANSFORM
LCALL DISPLAY
MOV DPTR, #0DFF8H
MOVX @DPTR, A
WAIT:
JB P3.3, WAIT
MOVX A, @DPTR
MOV NUM, A
LCALL DELAY10
JMP MAIN

;******* A->LED *******
TRANSFORM:
; A: 0-255
; T: 0-50˚C
; LED = A / 5.1
; 1. 十位, 直接除以51
MOV A, NUM
MOV B, #51
DIV AB; 此时a存储了10位，b存储了余数
MOV LED1, A
MOV A, B

; 2. 个位, 余数乘以10，范围是0000H-00FFH-01F5H
MOV B, #10
MUL AB
MOV R6, B
CJNE R6, #1, UNIT_L5
UNIT_G5:
; 情况1: 0100H-01F5H
; (int)((b+1)/51+5)
INC A
MOV B, #51
DIV AB; 此时
ADD A, #5
MOV LED2, A
AJMP DECIMAL
UNIT_L5:
; 情况2: 0000H-00FFH
DOTT:
MOV B, #51
DIV AB
MOV LED2,A

; 3.x小数位
DECIMAL:
MOV A, #10
MUL AB
MOV R6, B
CJNE R6, #1, DECIMAL_L5
DECIMAL_G5:
INC A
MOV B, #51
DIV AB
ADD A, #5
MOV LED4, A
AJMP TRANSFORM_END
DECIMAL_L5:
MOV B, #51
DIV AB
MOV LED4, A

; 四舍五入
ROUND:
MOV A, #10
MUL AB
MOV R6, B
CJNE R6, #1, TRANSFORM_END    ; 若小数点2位小于5，直接结束
; 小数点第二位大于5
MOV R6, LED4
; 第一个分支：如果小数位为9, 处理进个位，反之直接四舍五入
CJNE R6, #9, ROUND_CASE1
    ; 开始处理进个位
    MOV LED4, #0
    MOV R6, LED2
    ; 若个位又刚好又是9, 则进十位
    CJNE R6, #9, ROUND_CASE2
        MOV LED2, #0
        MOV R6, LED1
        INC R6
        MOV LED1, R6
        LJMP TRANSFORM_END
    ; 只进个位的情况
    ROUND_CASE2:
        INC R6
        MOV LED2, R6
        LJMP TRANSFORM_END
; 直接四舍五入的情况
ROUND_CASE1:
    INC R6
    MOV LED4, R6
TRANSFORM_END:
RET

;******* 显示子程序 *******
DISPLAY:
MOV A, LED1
ANL A, #0FH
MOV DPTR, #DSEG1
MOVC A, @A+DPTR
MOV DPTR, #7FF8H
MOVX @DPTR, A

MOV A, LED2
ANL A, #0FH
MOV DPTR, #DSEG1
MOVC A, @A+DPTR
MOV DPTR, #7FF9H
MOVX @DPTR, A

MOV A, LED3
MOV DPTR, #DSEG2
ANL A, #0FH
MOVC A, @A+DPTR
MOV DPTR, #7FFAH
MOVX @DPTR, A

MOV A, LED4
ANL A, #0FH
MOV DPTR, #DSEG1
MOVC A, @A+DPTR
MOV DPTR, #7FFBH
MOVX @DPTR, A

;******* 延时10ms *******
DELAY10:
NOP
NOP; 2*T
MOV R6, #5; 1*T
DEL2:
MOV R5, #98; 5*1*T
DEL1:
DJNZ R5, DEL1; 5*98*2*T
DJNZ R6, DEL2; 5*2*T
RET; 2*T

;******* 表 *******
DSEG1:
DB 0C0H, 0F9H, 0A4H, 0B0H
DB 99H, 92H, 82H, 0F8H, 80H
DB 090H, 88H, 83H, 0C6H, 0A1H
DB 86H, 8EH, 88H, 83H
DB 0C6H, 0A1H, 86H, 8EH

DSEG2:
DB 7FH

END
