
LED1 EQU 40H
LED2 EQU 41H
LED3 EQU 42H
LED4 EQU 43H

NUM EQU 50H
TMP1 EQU 51H
TMP2 EQU 52H
T_SET EQU 53H
T_REAL EQU 54H
T_DELTA EQU 55H

ORG 0000H
LJMP START
ORG 001BH
LJMP INT_1
ORG 0100H

START:
; 初始化变量
MOV LED1, #0
MOV LED2, #0
MOV LED3, #0
MOV LED4, #0
MOV T_SET, #0;      拨码盘的预设温度
MOV T_REAL, #0;     实测温度
MOV NUM, #0;        实际温度的数字量
MOV T_DELTA, #5;    温度差阈值
MOV SP, #60H;       设置堆栈地址, 以防冲突
; 设置中断
SETB EA;            允许中断
SETB EX1;           允许INT1
SETB IT1;           边缘触发
MOV DPTR, #0DFFAH
MOVX @DPTR, A;      开启ad转换

;******* 主程序 *******
MAIN:
LCALL READ_BCD;     读bcd盘
LCALL DISPLAY;      显示
; 比较温度
MOV A, T_SET
MOV R0, T_REAL
CLR CY
SUBB A, R0
JC HIGHERT
LOWERT:;            低于预设温度的情况
    MOV T_DELTA, A
    CLR CY
    SUBB A, #5
    JC USE_MODE2
    USE_MODE1:;     温差大于5摄氏度, 启用模式1
        LCALL HEATMODE1
        JMP ENDCMP_MAIN
    USE_MODE2:;     温差小于5摄氏度, 启用模式2
        LCALL HEATMODE2
        JMP ENDCMP_MAIN
HIGHERT:;           高于预设温度的情况, 启用模式3
    LCALL HEATMODE3
    JMP ENDCMP_MAIN
ENDCMP_MAIN:
LCALL DELAY10
JMP MAIN

;******* 中断程序 *******
INT_1:
; 开始堆栈
PUSH PSW
PUSH ACC
PUSH DPH;           DPTR入栈
PUSH DPL
LCALL READ_T;       读取温度
LCALL TRANS;        转换为模拟量
; 再次启动ad转换
MOV DPTR, #0DFFAH
MOVX @DPTR, A
; 出栈
POP DPL
POP DPH
POP ACC
POP PSW
RETI

;******* 读bcd *******
READ_BCD:
SETB P1.7
MOV DPTR, #0BFFFH
MOVX A, @DPTR
CPL A
; 读个位
MOV R0, A
ANL A, #0FH
MOV LED2, A
; 读十位
MOV A, R0
SWAP A
ANL A, #0FH
MOV LED1, A
; 存储为设定温度
MOV B, #10
MUL AB
ADD A, LED2
MOV T_SET, A
RET

;******* 加热 *******
HEATMODE1:
; mode1:持续加热
MOV DPTR, #7FFCH
MOV A, #01H
MOVX @DPTR, A
LCALL DELAY5000
RET

HEATMODE2:
; mode2 间断加热
MOV DPTR, #7FFCH
MOV A, #01H
MOVX @DPTR, A
LCALL DELAY1000
LCALL DELAY1000
MOV A, #00H
MOVX @DPTR, A
LCALL DELAY1000
LCALL DELAY1000
LCALL DELAY1000
RET

HEATMODE3:
; mode3 不加热
MOV DPTR, #7FFCH
MOV A, #00H
MOVX @DPTR, A
LCALL DELAY5000
RET

;******* AD转换 *******
TRANS:
; a = 0-255 : t = 0-100
; T = 2A / 51
; 处理10位, 先乘以2，再除以51
DECADE:
MOV A, NUM
MOV B, #2
MUL AB
MOV R5, B
CJNE R5, #1, DECADE_L5
DECADE_G5:
    MOV B, #51
    INC A
    DIV AB
    ADD A, #5
    MOV LED3, A
    AJMP CAL_UNIT
DECADE_L5:
    MOV B, #51
    DIV AB
    MOV LED3, A
    AJMP CAL_UNIT
; 处理个位, 先乘以10, 再除以51
CAL_UNIT:
MOV A, #10
MUL AB
MOV R5, B
CJNE R5, #1, UNIT_L5
UNIT_G5:
    MOV B, #51
    INC A
    DIV AB
    ADD A, #5
    MOV LED4, A
    AJMP ROUNDING
UNIT_L5:
    MOV B, #51
    DIV AB
    MOV LED4, A
    AJMP ROUNDING
; 四舍五入
ROUNDING:
; 判断是否需要5入
MOV A, #10
MUL AB
MOV R5, B
CJNE R5, #1, TRANS_END
MOV R5, LED4
CJNE R5, #9, ROUNDING_UNIT
; 需要10位进位, 个位清零的情况
ROUNDING_DECADE:
    MOV A, LED3
    INC A
    MOV LED3, A
    JMP TRANS_END
; 需要个位进位, 十位不变的情况
ROUNDING_UNIT:
    INC R5
    MOV LED4, R5
    JMP TRANS_END
; 将得到的十位, 个位保存
; 并计算保存温度的实际数值到T_REAL
TRANS_END:
MOV A, LED3
MOV B, #10
MUL AB
MOV B, LED4
ADD A, B
MOV T_REAL, A
RET

;******* 获取温度 *******
READ_T:
MOV DPTR, #0DFFAH;  通道2
MOVX A, @DPTR;      读取通道2的数字
MOV NUM, A;         存储
RET

;******* 显示子程序 *******
DISPLAY:
MOV A, LED1
ANL A, #0FH
MOV DPTR, #DSEG1
MOVC A, @A+DPTR
MOV DPTR, #7FF8H
MOVX @DPTR, A

MOV A, LED2
ANL A, #0FH
MOV DPTR, #DSEG1
MOVC A, @A+DPTR
MOV DPTR, #7FF9H
MOVX @DPTR, A

MOV A, LED3
MOV DPTR, #DSEG1
ANL A, #0FH
MOVC A, @A+DPTR
MOV DPTR, #7FFAH
MOVX @DPTR, A

MOV A, LED4
ANL A, #0FH
MOV DPTR, #DSEG1
MOVC A, @A+DPTR
MOV DPTR, #7FFBH
MOVX @DPTR, A
RET

;******* 延时10ms *******
DELAY10:
NOP
NOP; 2*T
MOV R6, #5; 1*T
DEL2:
MOV R5, #98; 5*1*T
DEL1:
DJNZ R5, DEL1; 5*98*2*T
DJNZ R6, DEL2; 5*2*T
RET; 2*T

;******* 定时器1s *******
DELAY1000:
SETB P1.6
MOV TMOD, #61H; T1:模式2; T0:模式1
MOV TH1, #0EBH
MOV TL1, #0EBH; N = 256-20 = 236 = 0EBH, 计20个脉冲
SETB TR1
DELAY1000_LOOP1:
    MOV TH0, #3CH
    MOV TL0, #0B0H; 50ms
    SETB TR0; 启动定时器T0
DELAY1000_LOOP2:
    JBC TF0, DELAY1000_LOOP3
    JMP DELAY1000_LOOP2
DELAY1000_LOOP3:
    CPL P1.6
    JBC TF1, DELAY1000_LOOP1
CLR TR0
CLR TR1
RET

;******* 定时器5s *******
DELAY5000:
SETB P1.6
MOV TMOD, #61H; T1:模式2; T0:模式1
MOV TH1, #9CH
MOV TL1, #9CH; N = 256-100 = 156 = 9CH, 计20个脉冲
SETB TR1
DELAY5000_LOOP1:
    MOV TH0, #3CH
    MOV TL0, #0B0H; 50ms
    SETB TR0; 启动定时器T0
DELAY5000_LOOP2:
    JBC TF0, DELAY5000_LOOP3
    JMP DELAY5000_LOOP2
DELAY5000_LOOP3:
    CPL P1.6
    JBC TF1, DELAY5000_LOOP1
CLR TR0
CLR TR1
RET

;******* 表 *******
DSEG1:
DB 0C0H, 0F9H, 0A4H, 0B0H
DB 99H, 92H, 82H, 0F8H, 80H
DB 090H, 88H, 83H, 0C6H, 0A1H
DB 86H, 8EH, 88H, 83H
DB 0C6H, 0A1H, 86H, 8EH

END
